000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. FACTURE.
000300
000400* GENERATION DES FACTURES A PARTIR DES DONNEES EXTRAITES
000500 ENVIRONMENT DIVISION.
000600 CONFIGURATION SECTION.
000700 SPECIAL-NAMES.
000800     DECIMAL-POINT IS COMMA.
000900
001000 INPUT-OUTPUT SECTION.
001100 FILE-CONTROL.
001200     SELECT EXTRACT-FILE ASSIGN TO EXTRACT
001300     ORGANIZATION IS SEQUENTIAL
001400     ACCESS MODE IS SEQUENTIAL
001500     FILE STATUS IS WS-EXTRACT-STATUS.
001600
001700     SELECT FACTURE-FILE ASSIGN TO FACTURES
001800     ORGANIZATION IS SEQUENTIAL
001900     ACCESS MODE IS SEQUENTIAL
002000     FILE STATUS IS WS-FACTURE-STATUS.
002100
002200 DATA DIVISION.
002300 FILE SECTION.
002400 FD  EXTRACT-FILE
002500     RECORDING MODE IS F
002600     RECORD CONTAINS 280 CHARACTERS.
002700 01  EXTRACT-RECORD.
002800     05 EXT-COMPANY           PIC X(30).
002900     05 EXT-ADDRESS           PIC X(100).
003000     05 EXT-CITY              PIC X(20).
003100     05 EXT-ZIP               PIC X(5).
003200     05 EXT-STATE             PIC X(2).
003300     05 EXT-O-NO              PIC 9(3).
003400     05 EXT-ODATE-ISO         PIC X(10).
003500     05 EXT-DNAME             PIC X(20).
003600     05 EXT-LNAME             PIC X(20).
003700     05 EXT-FNAME             PIC X(20).
003800     05 EXT-P-NO              PIC X(3).
003900     05 EXT-DESCRIPTION       PIC X(30).
004000     05 EXT-QUANTITY          PIC 9(2).
004100     05 EXT-PRICE             PIC 9(3).99.
004200     05 EXT-LINE-TOTAL        PIC 9(5).99.
004300     05 FILLER                PIC X(1).
004400
004500 FD  FACTURE-FILE
004600     RECORDING MODE IS F
004700     RECORD CONTAINS 132 CHARACTERS.
004800 01  FACTURE-RECORD           PIC X(132).
004900
005000 WORKING-STORAGE SECTION.
005100* VARIABLES DE CONTROLE DES FICHIERS
005200 77 WS-EXTRACT-STATUS         PIC XX.
005300 77 WS-FACTURE-STATUS         PIC XX.
005400 77 WS-EOF-EXTRACT            PIC X VALUE 'N'.
005500     88 EOF-EXTRACT           VALUE 'Y'.
005600
005700* VARIABLES POUR LA FACTURE EN COURS
005800 77 WS-CURRENT-ORDER          PIC 9(3) VALUE ZERO.
005900 77 WS-ORDER-TOTAL            PIC 9(7)V99 VALUE ZERO.
006000* TVA PAR DEFAUT A 20%
006100 77 WS-TVA-RATE               PIC 9V999 VALUE 0,200.
006200 77 WS-TVA-AMOUNT             PIC 9(7)V99 VALUE ZERO.
006300 77 WS-COMMISSION-RATE        PIC 9V999 VALUE 0,099.
006400 77 WS-COMMISSION-AMOUNT      PIC 9(7)V99 VALUE ZERO.
006500 77 WS-TOTAL-WITH-TAXES       PIC 9(7)V99 VALUE ZERO.
006600
006700* VARIABLES POUR LIRE LE TAUX DE TVA
006800 77 WS-TVA-INPUT              PIC X(10).
006900 77 WS-TVA-NUMERIC            PIC 9(2)V9.
007000 77 WS-ERROR-FLAG             PIC X VALUE 'N'.
007100     88 ERROR-OCCURRED        VALUE 'Y'.
007200
007300* VARIABLES DE TRAVAIL POUR LES CALCULS
007400 77 WS-LINE-TOTAL-WORK        PIC 9(7)V99.
007500 77 WS-PRICE-WORK             PIC 9(5)V99.
007600 77 WS-QUANTITY-WORK          PIC 9(2).
007700
007800* VARIABLES POUR LE FORMATAGE DE LA DATE
007900 77 WS-DATE-IN                PIC X(8).
008000 77 WS-DATE-FORMATEE          PIC X(40).
008100
008200* LIGNES DE SORTIE FORMATEES
008300 01 LIGNE-VIDE                PIC X(132) VALUE SPACES.
008400
008500 01 LIGNE-ADRESSE.
008600     05 FILLER                PIC X(50) VALUE SPACES.
008700     05 LA-COMPANY            PIC X(30).
008800     05 FILLER                PIC X(52) VALUE SPACES.
008900
009000 01 LIGNE-RUE.
009100     05 FILLER                PIC X(50) VALUE SPACES.
009200     05 LA-ADDRESS            PIC X(30).
009300     05 FILLER                PIC X(52) VALUE SPACES.
009400
009500 01 LIGNE-VILLE.
009600     05 FILLER                PIC X(50) VALUE SPACES.
009700     05 LA-CITY               PIC X(20).
009800     05 FILLER                PIC X(2) VALUE ', '.
009900     05 LA-ZIP                PIC X(5).
010000     05 FILLER                PIC X(55) VALUE SPACES.
010100
010200 01 LIGNE-STATE.
010300     05 FILLER                PIC X(50) VALUE SPACES.
010400     05 LA-STATE              PIC X(2).
010500     05 FILLER                PIC X(80) VALUE SPACES.
010600
010700 01 LIGNE-DATE.
010800     05 FILLER                PIC X(5) VALUE SPACES.
010900     05 LD-VILLE              PIC X(20) VALUE 'New York'.
011000     05 FILLER                PIC X(2) VALUE ', '.
011100     05 LD-DATE-FORMATEE      PIC X(40).
011200     05 FILLER                PIC X(65) VALUE SPACES.
011300
011400 01 LIGNE-ORDER.
011500     05 FILLER                PIC X(5) VALUE SPACES.
011600     05 FILLER                PIC X(16) VALUE 'Order Number : '.
011700     05 LO-ORDER-NO           PIC 999.
011800     05 FILLER                PIC X(108) VALUE SPACES.
011900
012000 01 LIGNE-ORDER-DATE.
012100     05 FILLER                PIC X(5) VALUE SPACES.
012200     05 FILLER                PIC X(14) VALUE 'Order Date : '.
012300     05 LOD-ORDER-DATE        PIC X(10).
012400     05 FILLER                PIC X(103) VALUE SPACES.
012500
012600 01 LIGNE-CONTACT.
012700     05 FILLER                PIC X(5) VALUE SPACES.
012800     05 FILLER                PIC X(35) VALUE
012900                          'Your contact within the department '.
013000     05 LC-DNAME              PIC X(20).
013100     05 FILLER                PIC X(3) VALUE ' : '.
013200     05 LC-LNAME              PIC X(20).
013300     05 FILLER                PIC X(2) VALUE ', '.
013400     05 LC-FNAME              PIC X(20).
013500     05 FILLER                PIC X(27) VALUE SPACES.
013600
013700 01 LIGNE-HEADER.
013800     05 FILLER                PIC X(5) VALUE SPACES.
013900     05 FILLER                PIC X(5) VALUE 'P_NO '.
014000     05 FILLER                PIC X(12) VALUE 'DESCRIPTION '.
014100     05 FILLER                PIC X(10) VALUE 'QUANTITY  '.
014200     05 FILLER                PIC X(8) VALUE 'PRICE   '.
014300     05 FILLER                PIC X(10) VALUE 'LINE TOTAL'.
014400     05 FILLER                PIC X(82) VALUE SPACES.
014500
014600 01 LIGNE-SEPARATEUR.
014700     05 FILLER                PIC X(5) VALUE SPACES.
014800     05 FILLER                PIC X(50) VALUE ALL '-'.
014900     05 FILLER                PIC X(77) VALUE SPACES.
015000
015100 01 LIGNE-PRODUIT.
015200     05 FILLER                PIC X(5) VALUE SPACES.
015300     05 LP-P-NO               PIC X(3).
015400     05 FILLER                PIC X(2) VALUE SPACES.
015500     05 LP-DESCRIPTION        PIC X(30).
015600     05 FILLER                PIC X(2) VALUE SPACES.
015700     05 LP-QUANTITY           PIC ZZ9.
015800     05 FILLER                PIC X(5) VALUE SPACES.
015900     05 LP-PRICE              PIC ZZZ,99.
016000     05 FILLER                PIC X(3) VALUE SPACES.
016100     05 LP-LINE-TOTAL         PIC ZZ.ZZZ,99.
016200     05 FILLER                PIC X(71) VALUE SPACES.
016300
016400 01 LIGNE-SOUS-TOTAL.
016500     05 FILLER                PIC X(70) VALUE SPACES.
016600     05 FILLER                PIC X(12) VALUE 'SUB TOTAL : '.
016700     05 LST-AMOUNT            PIC ZZ.ZZZ,99.
016800     05 FILLER                PIC X(41) VALUE SPACES.
016900
017000 01 LIGNE-TVA.
017100     05 FILLER                PIC X(70) VALUE SPACES.
017200     05 FILLER                PIC X(12) VALUE 'SALES TAX : '.
017300     05 LT-AMOUNT             PIC ZZ.ZZZ,99.
017400     05 FILLER                PIC X(41) VALUE SPACES.
017500
017600 01 LIGNE-COMMISSION.
017700     05 FILLER                PIC X(70) VALUE SPACES.
017800     05 FILLER                PIC X(12) VALUE 'COMMISSION: '.
017900     05 LC-AMOUNT             PIC ZZ.ZZZ,99.
018000     05 FILLER                PIC X(41) VALUE SPACES.
018100
018200 01 LIGNE-TOTAL.
018300     05 FILLER                PIC X(70) VALUE SPACES.
018400     05 FILLER                PIC X(12) VALUE 'TOTAL     : '.
018500     05 LTO-AMOUNT            PIC ZZ.ZZZ,99.
018600     05 FILLER                PIC X(41) VALUE SPACES.
018700
018800 PROCEDURE DIVISION.
018900
019000     DISPLAY "=== DEBUT GENERATION FACTURES ==="
019100
019200* LECTURE DU TAUX DE TVA DEPUIS SYSIN
019300     PERFORM LIRE-TAUX-TVA
019400
019500* OUVERTURE DES FICHIERS
019600     OPEN INPUT EXTRACT-FILE
019700     IF WS-EXTRACT-STATUS NOT = '00'
019800         DISPLAY "ERREUR OUVERTURE EXTRACT : ", WS-EXTRACT-STATUS
019900         PERFORM ABEND-PROG
020000     END-IF
020100
020200     OPEN OUTPUT FACTURE-FILE
020300     IF WS-FACTURE-STATUS NOT = '00'
020400         DISPLAY "ERREUR OUVERTURE FACTURES : ", WS-FACTURE-STATUS
020500         PERFORM ABEND-PROG
020600     END-IF
020700
020800* LECTURE DU PREMIER ENREGISTREMENT
020900     PERFORM LIRE-EXTRACT
021000
021100* TRAITEMENT PRINCIPAL
021200     PERFORM UNTIL EOF-EXTRACT
021300         PERFORM GENERER-FACTURE
021400     END-PERFORM
021500
021600* FERMETURE DES FICHIERS
021700     CLOSE EXTRACT-FILE
021800     CLOSE FACTURE-FILE
021900
022000     DISPLAY "=== FIN GENERATION FACTURES ==="
022100     GOBACK.
022200
022300 LIRE-TAUX-TVA.
022400* LECTURE DU TAUX DE TVA DEPUIS SYSIN
022500* FORMATS SIMPLES: 20 (pour 20%) ou format dÃ©cimal
022600     MOVE 'N' TO WS-ERROR-FLAG
022700
022800     ACCEPT WS-TVA-INPUT FROM SYSIN
022900     DISPLAY "TVA LUE DEPUIS SYSIN: '", WS-TVA-INPUT, "'"
023000
023100* CONVERSION SIMPLE - LE SYSTEME GERE LES FORMATS
023200     IF WS-TVA-INPUT IS NUMERIC
023300         MOVE WS-TVA-INPUT TO WS-TVA-NUMERIC
023400*        IF WS-TVA-NUMERIC > 0 AND WS-TVA-NUMERIC < 100
023410         IF WS-TVA-NUMERIC > 0
023500* CONVERSION POURCENTAGE VERS DECIMALE
023600             COMPUTE WS-TVA-RATE = WS-TVA-NUMERIC / 100
023700             DISPLAY "TAUX TVA APPLIQUE : ", WS-TVA-NUMERIC, "%"
023800         ELSE
023900             SET ERROR-OCCURRED TO TRUE
024000             DISPLAY "ERREUR: TAUX HORS LIMITES (0-99%)"
024100         END-IF
024200     ELSE
024300         SET ERROR-OCCURRED TO TRUE
024400         DISPLAY "ERREUR: FORMAT INVALIDE"
024500     END-IF
024600
024700* EN CAS D'ERREUR, UTILISER 20% PAR DEFAUT
024800     IF ERROR-OCCURRED
024900         MOVE 0,200 TO WS-TVA-RATE
025000         DISPLAY "UTILISATION DU TAUX PAR DEFAUT : 20%"
025100     END-IF
025200
025300     DISPLAY "TAUX TVA FINAL UTILISE : ", WS-TVA-RATE
025400     .
025500
025600 LIRE-EXTRACT.
025700     READ EXTRACT-FILE
025800         AT END
025900             SET EOF-EXTRACT TO TRUE
026000         NOT AT END
026100             CONTINUE
026200     END-READ
026300     .
026400
026500 GENERER-FACTURE.
026600* INITIALISATION POUR UNE NOUVELLE FACTURE
026700     MOVE EXT-O-NO TO WS-CURRENT-ORDER
026800     MOVE ZERO TO WS-ORDER-TOTAL
026900
027000* EN-TETE DE LA FACTURE
027100     PERFORM ECRIRE-ENTETE-FACTURE
027200
027300* LIGNES DE PRODUITS POUR CETTE COMMANDE
027400     PERFORM UNTIL EOF-EXTRACT OR EXT-O-NO NOT = WS-CURRENT-ORDER
027500         PERFORM ECRIRE-LIGNE-PRODUIT
027600* CONVERSION DU CHAMP EDITE VERS NUMERIQUE POUR LE CALCUL
027700         MOVE EXT-LINE-TOTAL TO WS-LINE-TOTAL-WORK
027800         ADD WS-LINE-TOTAL-WORK TO WS-ORDER-TOTAL
027900         PERFORM LIRE-EXTRACT
028000     END-PERFORM
028100
028200* CALCULS ET TOTAUX
028300     PERFORM CALCULER-TOTAUX
028400     PERFORM ECRIRE-TOTAUX
028500
028600* SAUT DE PAGE
028700     PERFORM ECRIRE-SAUT-PAGE
028800     .
028900
029000 ECRIRE-ENTETE-FACTURE.
029100* ADRESSE SOCIETE
029200     MOVE EXT-COMPANY TO LA-COMPANY
029300     MOVE LIGNE-ADRESSE TO FACTURE-RECORD
029400     PERFORM ECRIRE-LIGNE-FACTURE
029500
029600     MOVE EXT-ADDRESS TO LA-ADDRESS
029700     MOVE LIGNE-RUE TO FACTURE-RECORD
029800     PERFORM ECRIRE-LIGNE-FACTURE
029900
030000     MOVE EXT-CITY TO LA-CITY
030100     MOVE EXT-ZIP TO LA-ZIP
030200     MOVE LIGNE-VILLE TO FACTURE-RECORD
030300     PERFORM ECRIRE-LIGNE-FACTURE
030400
030500     MOVE EXT-STATE TO LA-STATE
030600     MOVE LIGNE-STATE TO FACTURE-RECORD
030700     PERFORM ECRIRE-LIGNE-FACTURE
030800
030900     PERFORM ECRIRE-LIGNE-VIDE
031000     PERFORM ECRIRE-LIGNE-VIDE
031100
031200* DATE ET LIEU
031300     MOVE EXT-ODATE-ISO TO WS-DATE-IN
031400     CALL 'DATEFMT' USING BY REFERENCE WS-DATE-IN
031500                          BY REFERENCE WS-DATE-FORMATEE
031600     MOVE WS-DATE-FORMATEE TO LD-DATE-FORMATEE
031700     MOVE LIGNE-DATE TO FACTURE-RECORD
031800     PERFORM ECRIRE-LIGNE-FACTURE
031900
032000     PERFORM ECRIRE-LIGNE-VIDE
032100
032200* NUMERO DE COMMANDE
032300     MOVE EXT-O-NO TO LO-ORDER-NO
032400     MOVE LIGNE-ORDER TO FACTURE-RECORD
032500     PERFORM ECRIRE-LIGNE-FACTURE
032600
032700* DATE DE COMMANDE
032800     MOVE EXT-ODATE-ISO TO LOD-ORDER-DATE
032900     MOVE LIGNE-ORDER-DATE TO FACTURE-RECORD
033000     PERFORM ECRIRE-LIGNE-FACTURE
033100
033200     PERFORM ECRIRE-LIGNE-VIDE
033300
033400* CONTACT
033500     MOVE EXT-DNAME TO LC-DNAME
033600     MOVE EXT-LNAME TO LC-LNAME
033700     MOVE EXT-FNAME TO LC-FNAME
033800     MOVE LIGNE-CONTACT TO FACTURE-RECORD
033900     PERFORM ECRIRE-LIGNE-FACTURE
034000
034100     PERFORM ECRIRE-LIGNE-VIDE
034200     PERFORM ECRIRE-LIGNE-VIDE
034300
034400* EN-TETE DU TABLEAU
034500     MOVE LIGNE-HEADER TO FACTURE-RECORD
034600     PERFORM ECRIRE-LIGNE-FACTURE
034700
034800     MOVE LIGNE-SEPARATEUR TO FACTURE-RECORD
034900     PERFORM ECRIRE-LIGNE-FACTURE
035000     .
035100
035200 ECRIRE-LIGNE-PRODUIT.
035300     MOVE EXT-P-NO TO LP-P-NO
035400     MOVE EXT-DESCRIPTION TO LP-DESCRIPTION
035500* CONVERSION DES CHAMPS EDITES POUR L'AFFICHAGE
035600     MOVE EXT-QUANTITY TO WS-QUANTITY-WORK
035700     MOVE WS-QUANTITY-WORK TO LP-QUANTITY
035800     MOVE EXT-PRICE TO WS-PRICE-WORK
035900     MOVE WS-PRICE-WORK TO LP-PRICE
036000     MOVE EXT-LINE-TOTAL TO WS-LINE-TOTAL-WORK
036100     MOVE WS-LINE-TOTAL-WORK TO LP-LINE-TOTAL
036200
036300     MOVE LIGNE-PRODUIT TO FACTURE-RECORD
036400     PERFORM ECRIRE-LIGNE-FACTURE
036500     .
036600
036700 CALCULER-TOTAUX.
036800* CALCUL DE LA TVA
036900     COMPUTE WS-TVA-AMOUNT = WS-ORDER-TOTAL * WS-TVA-RATE
037000
037100* CALCUL DE LA COMMISSION
037200     COMPUTE WS-COMMISSION-AMOUNT = WS-ORDER-TOTAL
037300                                            * WS-COMMISSION-RATE
037400
037500* CALCUL DU TOTAL AVEC TAXES
037600     COMPUTE WS-TOTAL-WITH-TAXES = WS-ORDER-TOTAL
037700                          + WS-TVA-AMOUNT + WS-COMMISSION-AMOUNT
037800     .
037900
038000 ECRIRE-TOTAUX.
038100     MOVE LIGNE-SEPARATEUR TO FACTURE-RECORD
038200     PERFORM ECRIRE-LIGNE-FACTURE
038300
038400* SOUS-TOTAL
038500     MOVE WS-ORDER-TOTAL TO LST-AMOUNT
038600     MOVE LIGNE-SOUS-TOTAL TO FACTURE-RECORD
038700     PERFORM ECRIRE-LIGNE-FACTURE
038800
038900* TVA
039000     MOVE WS-TVA-AMOUNT TO LT-AMOUNT
039100     MOVE LIGNE-TVA TO FACTURE-RECORD
039200     PERFORM ECRIRE-LIGNE-FACTURE
039300
039400* COMMISSION
039500     MOVE WS-COMMISSION-AMOUNT TO LC-AMOUNT
039600     MOVE LIGNE-COMMISSION TO FACTURE-RECORD
039700     PERFORM ECRIRE-LIGNE-FACTURE
039800
039900     PERFORM ECRIRE-LIGNE-VIDE
040000
040100* TOTAL
040200     MOVE WS-TOTAL-WITH-TAXES TO LTO-AMOUNT
040300     MOVE LIGNE-TOTAL TO FACTURE-RECORD
040400     PERFORM ECRIRE-LIGNE-FACTURE
040500     .
040600
040700 ECRIRE-LIGNE-FACTURE.
040800     WRITE FACTURE-RECORD
040900     IF WS-FACTURE-STATUS NOT = '00'
041000         DISPLAY "ERREUR ECRITURE FACTURE : ", WS-FACTURE-STATUS
041100         PERFORM ABEND-PROG
041200     END-IF
041300     .
041400
041500 ECRIRE-LIGNE-VIDE.
041600     MOVE LIGNE-VIDE TO FACTURE-RECORD
041700     PERFORM ECRIRE-LIGNE-FACTURE
041800     .
041900
042000 ECRIRE-SAUT-PAGE.
042100* ECRITURE DE QUELQUES LIGNES VIDES POUR SEPARATION
042200     PERFORM ECRIRE-LIGNE-VIDE
042300     PERFORM ECRIRE-LIGNE-VIDE
042400     PERFORM ECRIRE-LIGNE-VIDE
042500     PERFORM ECRIRE-LIGNE-VIDE
042600     PERFORM ECRIRE-LIGNE-VIDE
042700     .
042800
042900 ABEND-PROG.
043000     DISPLAY "ARRET ANORMAL DU PROGRAMME"
043100     MOVE 16 TO RETURN-CODE
043200     GOBACK
043300     .
