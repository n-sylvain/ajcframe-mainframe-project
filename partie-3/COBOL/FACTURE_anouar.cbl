000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. FACTURE.
000300
000400* GENERATION DES FACTURES A PARTIR DES DONNEES EXTRAITES
000500 ENVIRONMENT DIVISION.
000600 CONFIGURATION SECTION.
000700 SPECIAL-NAMES.
000800     DECIMAL-POINT IS COMMA.
000900
001000 INPUT-OUTPUT SECTION.
001100 FILE-CONTROL.
001200     SELECT EXTRACT-FILE ASSIGN TO EXTRACT
001300     ORGANIZATION IS SEQUENTIAL
001400     ACCESS MODE IS SEQUENTIAL
001500     FILE STATUS IS WS-EXTRACT-STATUS.
001600
001700     SELECT FACTURE-FILE ASSIGN TO FACTURES
001800     ORGANIZATION IS SEQUENTIAL
001900     ACCESS MODE IS SEQUENTIAL
002000     FILE STATUS IS WS-FACTURE-STATUS.
002100
002200 DATA DIVISION.
002300 FILE SECTION.
002400 FD  EXTRACT-FILE
002500     RECORDING MODE IS F
002600     RECORD CONTAINS 281 CHARACTERS.
002700 01  EXTRACT-RECORD.
002800     05 EXT-COMPANY           PIC X(30).
002900     05 EXT-ADDRESS           PIC X(100).
003000     05 EXT-CITY              PIC X(20).
003100     05 EXT-ZIP               PIC X(5).
003200     05 EXT-STATE             PIC X(2).
003300     05 EXT-O-NO              PIC 9(3).
003400     05 EXT-ODATE-ISO         PIC X(10).
003500     05 EXT-DNAME             PIC X(20).
003600     05 EXT-LNAME             PIC X(20).
003700     05 EXT-FNAME             PIC X(20).
003710     05 EXT-COM               PIC 9V99.
003800     05 EXT-P-NO              PIC X(3).
003900     05 EXT-DESCRIPTION       PIC X(30).
004000     05 EXT-QUANTITY          PIC 9(2).
004100     05 EXT-PRICE             PIC 9(3)V99.
004200     05 EXT-LINE-TOTAL        PIC 9(5)V99.
004300     05 FILLER                PIC X(1).
004400
004500 FD  FACTURE-FILE
004600     RECORDING MODE IS F
004700     RECORD CONTAINS 132 CHARACTERS.
004800 01  FACTURE-RECORD           PIC X(132).
004900
005000 WORKING-STORAGE SECTION.
005100* VARIABLES DE CONTROLE DES FICHIERS
005200 77 WS-EXTRACT-STATUS         PIC XX.
005300 77 WS-FACTURE-STATUS         PIC XX.
005400 77 WS-EOF-EXTRACT            PIC X VALUE 'N'.
005500     88 EOF-EXTRACT           VALUE 'Y'.
005600
005700* VARIABLES POUR LA FACTURE EN COURS
005800 77 WS-CURRENT-ORDER          PIC 9(3) VALUE ZERO.
005900 77 WS-ORDER-TOTAL            PIC 9(7)V99 VALUE ZERO.
006000* TVA PAR DEFAUT A 20%
006100 77 WS-TVA-RATE               PIC 9V999 VALUE 0,200.
006200 77 WS-TVA-AMOUNT             PIC 9(7)V99 VALUE ZERO.
006300 77 WS-COMMISSION-RATE        PIC 9V99 VALUE 0,05.
006400 77 WS-COMMISSION-AMOUNT      PIC 9(7)V99 VALUE ZERO.
006500 77 WS-TOTAL-WITH-TAXES       PIC 9(7)V99 VALUE ZERO.
006600
006700* VARIABLES POUR LIRE LE TAUX DE TVA
006800 77 WS-TVA-INPUT              PIC X(10).
006900 77 WS-TVA-NUMERIC            PIC 9(2)V9.
007000 77 WS-ERROR-FLAG             PIC X VALUE 'N'.
007100     88 ERROR-OCCURRED        VALUE 'Y'.
007200 77 WS-TVA-PERCENT            PIC 9(2)V9.
007300
007400* VARIABLES DE TRAVAIL POUR LES CALCULS
007500 77 WS-LINE-TOTAL-WORK        PIC 9(7)V99.
007600 77 WS-PRICE-WORK             PIC 9(5)V99.
007700 77 WS-QUANTITY-WORK          PIC 9(2).
007800
007900* VARIABLES POUR LE FORMATAGE DE LA DATE
008000 77 WS-DATE-IN                PIC X(8) VALUE SPACES.
008100 77 WS-DATE-FORMATEE          PIC X(40).
008200
008300* LIGNES DE SORTIE FORMATEES - LARGEUR 119
008400 01 LIGNE-VIDE                PIC X(119) VALUE SPACES.
008500
008600 01 LIGNE-CADRE-HAUT.
008700     05 FILLER                PIC X(1) VALUE '+'.
008800     05 FILLER                PIC X(117) VALUE ALL '-'.
008900     05 FILLER                PIC X(1) VALUE '+'.
009000
009100 01 LIGNE-CADRE-BAS.
009200     05 FILLER                PIC X(1) VALUE '+'.
009300     05 FILLER                PIC X(117) VALUE ALL '-'.
009400     05 FILLER                PIC X(1) VALUE '+'.
009500
009600 01 LIGNE-CADRE-ADRESSE-HAUT.
009700     05 FILLER                PIC X(1) VALUE '|'.
009800     05 FILLER                PIC X(78) VALUE SPACES.
009900     05 FILLER                PIC X(1) VALUE '+'.
010000     05 FILLER                PIC X(34) VALUE ALL '-'.
010100     05 FILLER                PIC X(1) VALUE '+'.
010200     05 FILLER                PIC X(3) VALUE SPACES.
010300     05 FILLER                PIC X(1) VALUE '|'.
010400
010500 01 LIGNE-ADRESSE.
010600     05 FILLER                PIC X(1) VALUE '|'.
010700     05 FILLER                PIC X(78) VALUE SPACES.
010800     05 FILLER                PIC X(1) VALUE '|'.
010900     05 FILLER                PIC X(4) VALUE SPACES.
011000     05 LA-COMPANY            PIC X(27).
011100     05 FILLER                PIC X(3) VALUE SPACES.
011200     05 FILLER                PIC X(1) VALUE '|'.
011300     05 FILLER                PIC X(3) VALUE SPACES.
011400     05 FILLER                PIC X(1) VALUE '|'.
011500
011600 01 LIGNE-RUE.
011700     05 FILLER                PIC X(1) VALUE '|'.
011800     05 FILLER                PIC X(78) VALUE SPACES.
011900     05 FILLER                PIC X(1) VALUE '|'.
012000     05 FILLER                PIC X(4) VALUE SPACES.
012100     05 LA-ADDRESS            PIC X(27).
012200     05 FILLER                PIC X(3) VALUE SPACES.
012300     05 FILLER                PIC X(1) VALUE '|'.
012400     05 FILLER                PIC X(3) VALUE SPACES.
012500     05 FILLER                PIC X(1) VALUE '|'.
012600
012700 01 LIGNE-VILLE.
012800     05 FILLER                PIC X(1) VALUE '|'.
012900     05 FILLER                PIC X(78) VALUE SPACES.
013000     05 FILLER                PIC X(1) VALUE '|'.
013100     05 FILLER                PIC X(4) VALUE SPACES.
013200     05 LA-CITY               PIC X(20).
013400     05 FILLER                PIC X(2) VALUE ', '.
013500     05 LA-ZIP                PIC X(5).
013600     05 FILLER                PIC X(3) VALUE SPACES.
013700     05 FILLER                PIC X(1) VALUE '|'.
013800     05 FILLER                PIC X(3) VALUE SPACES.
013900     05 FILLER                PIC X(1) VALUE '|'.
014000
014100 01 LIGNE-STATE.
014200     05 FILLER                PIC X(1) VALUE '|'.
014300     05 FILLER                PIC X(78) VALUE SPACES.
014400     05 FILLER                PIC X(1) VALUE '|'.
014500     05 FILLER                PIC X(4) VALUE SPACES.
014600     05 LA-STATE              PIC X(2).
014700     05 FILLER                PIC X(28) VALUE SPACES.
014800     05 FILLER                PIC X(1) VALUE '|'.
014900     05 FILLER                PIC X(3) VALUE SPACES.
015000     05 FILLER                PIC X(1) VALUE '|'.
015100
015200 01 LIGNE-CADRE-ADRESSE-BAS.
015300     05 FILLER                PIC X(1) VALUE '|'.
015400     05 FILLER                PIC X(78) VALUE SPACES.
015500     05 FILLER                PIC X(35) VALUE ALL '-'.
015600     05 FILLER                PIC X(1) VALUE '+'.
015700     05 FILLER                PIC X(3) VALUE SPACES.
015800     05 FILLER                PIC X(1) VALUE '|'.
015900
016000 01 LIGNE-CADRE-VIDE.
016100     05 FILLER                PIC X(1) VALUE '|'.
016200     05 FILLER                PIC X(117) VALUE SPACES.
016300     05 FILLER                PIC X(1) VALUE '|'.
016400
016500 01 LIGNE-DATE.
016600     05 FILLER                PIC X(1) VALUE '|'.
016700     05 FILLER                PIC X(4) VALUE SPACES.
016800     05 LD-VILLE              PIC X(8) VALUE 'New York'.
016900     05 FILLER                PIC X(2) VALUE ', '.
017000     05 LD-DATE-FORMATEE      PIC X(40).
017100     05 FILLER                PIC X(63) VALUE SPACES.
017200     05 FILLER                PIC X(1) VALUE '|'.
017300
017400 01 LIGNE-ORDER.
017500     05 FILLER                PIC X(1) VALUE '|'.
017600     05 FILLER                PIC X(4) VALUE SPACES.
017700     05 FILLER                PIC X(16) VALUE 'Order Number : '.
017800     05 LO-ORDER-NO           PIC ZZZ.
017900     05 FILLER                PIC X(94) VALUE SPACES.
018000     05 FILLER                PIC X(1) VALUE '|'.
018100
018200 01 LIGNE-ORDER-DATE.
018300     05 FILLER                PIC X(1) VALUE '|'.
018400     05 FILLER                PIC X(4) VALUE SPACES.
018500     05 FILLER                PIC X(16) VALUE 'Order Date   : '.
018600     05 LOD-ORDER-DATE        PIC X(10).
018700     05 FILLER                PIC X(87) VALUE SPACES.
018800     05 FILLER                PIC X(1) VALUE '|'.
018900
019000 01 LIGNE-CONTACT.
019100     05 FILLER                PIC X(1) VALUE '|'.
019200     05 FILLER                PIC X(4) VALUE SPACES.
019300     05 FILLER                PIC X(35) VALUE
019400                          'Your contact within the department '.
019500     05 LC-DNAME              PIC X(20).
019600     05 FILLER                PIC X(3) VALUE ' : '.
019700     05 LC-LNAME              PIC X(20).
019800     05 FILLER                PIC X(2) VALUE ', '.
019900     05 LC-FNAME              PIC X(20).
020000     05 FILLER                PIC X(13) VALUE SPACES.
020100     05 FILLER                PIC X(1) VALUE '|'.
020200
020300 01 LIGNE-TABLEAU-HAUT.
020400     05 FILLER                PIC X(1) VALUE '|'.
020500     05 FILLER                PIC X(3) VALUE SPACES.
020600     05 FILLER                PIC X(1) VALUE '+'.
020700     05 FILLER                PIC X(109) VALUE ALL '-'.
020800     05 FILLER                PIC X(1) VALUE '+'.
020900     05 FILLER                PIC X(3) VALUE SPACES.
021000     05 FILLER                PIC X(1) VALUE '|'.
021100
021200 01 LIGNE-HEADER.
021300     05 FILLER                PIC X(1) VALUE '|'.
021400     05 FILLER                PIC X(3) VALUE SPACES.
021500     05 FILLER                PIC X(1) VALUE '|'.
021600     05 FILLER                PIC X(2) VALUE SPACES.
021700     05 FILLER                PIC X(4) VALUE 'P_NO'.
021800     05 FILLER                PIC X(1) VALUE ' '.
021900     05 FILLER                PIC X(11) VALUE 'DESCRIPTION'.
022000     05 FILLER                PIC X(26) VALUE SPACES.
022100     05 FILLER                PIC X(8) VALUE 'QUANTITY'.
022200     05 FILLER                PIC X(7) VALUE SPACES.
022300     05 FILLER                PIC X(5) VALUE 'PRICE'.
022400     05 FILLER                PIC X(3) VALUE SPACES.
022500     05 FILLER                PIC X(10) VALUE 'LINE TOTAL'.
022600     05 FILLER                PIC X(32) VALUE SPACES.
022700     05 FILLER                PIC X(1) VALUE '|'.
022800     05 FILLER                PIC X(3) VALUE SPACES.
022900     05 FILLER                PIC X(1) VALUE '|'.
023000
023100 01 LIGNE-PRODUIT.
023200     05 FILLER                PIC X(1) VALUE '|'.
023300     05 FILLER                PIC X(3) VALUE SPACES.
023400     05 FILLER                PIC X(1) VALUE '|'.
023500     05 FILLER                PIC X(2) VALUE SPACES.
023600     05 LP-P-NO               PIC X(3).
023700     05 FILLER                PIC X(2) VALUE SPACES.
023800     05 LP-DESCRIPTION        PIC X(25).
023900     05 FILLER                PIC X(6) VALUE SPACES.
024000     05 LP-QUANTITY           PIC ZZ9.
024100     05 FILLER                PIC X(6) VALUE SPACES.
024200     05 LP-PRICE              PIC ZZZ,99.
024300     05 FILLER                PIC X(7) VALUE SPACES.
024400     05 LP-LINE-TOTAL         PIC Z(5),99.
024500     05 FILLER                PIC X(41) VALUE SPACES.
024600     05 FILLER                PIC X(1) VALUE '|'.
024700     05 FILLER                PIC X(3) VALUE SPACES.
024800     05 FILLER                PIC X(1) VALUE '|'.
024900
025000 01 LIGNE-TABLEAU-BAS.
025100     05 FILLER                PIC X(1) VALUE '|'.
025200     05 FILLER                PIC X(3) VALUE SPACES.
025300     05 FILLER                PIC X(1) VALUE '+'.
025400     05 FILLER                PIC X(109) VALUE ALL '-'.
025500     05 FILLER                PIC X(1) VALUE '+'.
025600     05 FILLER                PIC X(3) VALUE SPACES.
025700     05 FILLER                PIC X(1) VALUE '|'.
025800
025900 01 LIGNE-SOUS-TOTAL.
026000     05 FILLER                PIC X(1) VALUE '|'.
026100     05 FILLER                PIC X(82) VALUE SPACES.
026200     05 FILLER                PIC X(15) VALUE 'SUB TOTAL    : '.
026300     05 LST-AMOUNT            PIC ZZ.ZZZ,99.
026400     05 FILLER                PIC X(11) VALUE SPACES.
026500     05 FILLER                PIC X(1) VALUE '|'.
026600
026700 01 LIGNE-TVA.
026800     05 FILLER                PIC X(1) VALUE '|'.
026900     05 FILLER                PIC X(78) VALUE SPACES.
027000     05 FILLER                PIC X(11) VALUE 'SALES TAX ('.
027100     05 LT-PERCENT            PIC Z9,9.
027200     05 FILLER                PIC X(4) VALUE '%): '.
027300     05 LT-AMOUNT             PIC ZZ.ZZZ,99.
027400     05 FILLER                PIC X(11) VALUE SPACES.
027500     05 FILLER                PIC X(1) VALUE '|'.
027600
027700 01 LIGNE-COMMISSION.
027800     05 FILLER                PIC X(1) VALUE '|'.
027900     05 FILLER                PIC X(77) VALUE SPACES.
028000     05 FILLER                PIC X(12) VALUE
028010                                       'COMMISSION ('.
028020     05 LC-COM                PIC 9,99.
028030     05 FILLER                PIC X(4) VALUE '%): '.
028100     05 LC-AMOUNT             PIC ZZ.ZZZ,99.
028200     05 FILLER                PIC X(11) VALUE SPACES.
028300     05 FILLER                PIC X(1) VALUE '|'.
028400
028500 01 LIGNE-TOTAL.
028600     05 FILLER                PIC X(1) VALUE '|'.
028700     05 FILLER                PIC X(65) VALUE SPACES.
028800     05 FILLER PIC X(32) VALUE 'TOTAL (SUB TOTAL + SALES TAX) : '.
028900     05 LTO-AMOUNT            PIC ZZ.ZZZ,99.
029000     05 FILLER                PIC X(11) VALUE SPACES.
029100     05 FILLER                PIC X(1) VALUE '|'.
029200
029300 PROCEDURE DIVISION.
029400
029500     DISPLAY "=== DEBUT GENERATION FACTURES ==="
029600
029700* OBTENIR LA DATE COURANTE FORMATEE
029800     PERFORM OBTENIR-DATE
029900
030000* LECTURE DU TAUX DE TVA DEPUIS SYSIN
030100     PERFORM LIRE-TAUX-TVA
030200
030300* OUVERTURE DES FICHIERS
030400     OPEN INPUT EXTRACT-FILE
030500     IF WS-EXTRACT-STATUS NOT = '00'
030600         DISPLAY "ERREUR OUVERTURE EXTRACT : ", WS-EXTRACT-STATUS
030700         PERFORM ABEND-PROG
030800     END-IF
030900
031000     OPEN OUTPUT FACTURE-FILE
031100     IF WS-FACTURE-STATUS NOT = '00'
031200         DISPLAY "ERREUR OUVERTURE FACTURES : ", WS-FACTURE-STATUS
031300         PERFORM ABEND-PROG
031400     END-IF
031500
031600* LECTURE DU PREMIER ENREGISTREMENT
031700     PERFORM LIRE-EXTRACT
031800
031900* TRAITEMENT PRINCIPAL
032000     PERFORM UNTIL EOF-EXTRACT
032100         PERFORM GENERER-FACTURE
032200     END-PERFORM
032300
032400* FERMETURE DES FICHIERS
032500     CLOSE EXTRACT-FILE
032600     CLOSE FACTURE-FILE
032700
032800     DISPLAY "=== FIN GENERATION FACTURES ==="
032900     GOBACK.
033000
033100 OBTENIR-DATE.
033200     CALL 'DATEFMT' USING BY REFERENCE WS-DATE-IN
033300                          BY REFERENCE WS-DATE-FORMATEE
033400     MOVE 'N' TO WS-ERROR-FLAG
033500     DISPLAY 'Date facture   : ' WS-DATE-FORMATEE
033600     .
033700
033800 LIRE-TAUX-TVA.
033900* LECTURE DU TAUX DE TVA DEPUIS SYSIN
034000     ACCEPT WS-TVA-INPUT FROM SYSIN
034100     DISPLAY "TVA LUE DEPUIS SYSIN: '", WS-TVA-INPUT, "'"
034200
034300* UTILISER FUNCTION NUMVAL POUR CONVERTIR CHAINE EN NUMERIQUE
034400     COMPUTE WS-TVA-NUMERIC = FUNCTION NUMVAL(WS-TVA-INPUT)
034500
034600     IF WS-TVA-NUMERIC > 0
034700* CONVERSION POURCENTAGE VERS DECIMALE
034800         COMPUTE WS-TVA-RATE = WS-TVA-NUMERIC / 100
034900         COMPUTE WS-TVA-PERCENT = WS-TVA-NUMERIC
035000         DISPLAY "TAUX TVA APPLIQUE : ", WS-TVA-NUMERIC, "%"
035100     ELSE
035200         SET ERROR-OCCURRED TO TRUE
035300         DISPLAY "ERREUR: TAUX HORS LIMITES (0-100%)"
035400     END-IF
035500
035600* EN CAS D'ERREUR, UTILISER 20% PAR DEFAUT
035700     IF ERROR-OCCURRED
035800         MOVE 0,200 TO WS-TVA-RATE
035900         MOVE 20 TO WS-TVA-PERCENT
036000         DISPLAY "UTILISATION DU TAUX PAR DEFAUT : 20%"
036100     END-IF
036200
036300     DISPLAY "TAUX TVA FINAL UTILISE : ", WS-TVA-RATE
036400     .
036500
036600 LIRE-EXTRACT.
036700     READ EXTRACT-FILE
036800         AT END
036900             SET EOF-EXTRACT TO TRUE
037000         NOT AT END
037100             CONTINUE
037200     END-READ
037300     .
037400
037500 GENERER-FACTURE.
037600* INITIALISATION POUR UNE NOUVELLE FACTURE
037700     MOVE EXT-O-NO TO WS-CURRENT-ORDER
037800     MOVE ZERO TO WS-ORDER-TOTAL
037900
038000* EN-TETE DE LA FACTURE AVEC CADRE
038100     MOVE LIGNE-CADRE-HAUT TO FACTURE-RECORD
038200     PERFORM ECRIRE-LIGNE-FACTURE
038300
038400     PERFORM ECRIRE-ENTETE-FACTURE
038500
038600* LIGNES DE PRODUITS POUR CETTE COMMANDE
038700     PERFORM UNTIL EOF-EXTRACT OR EXT-O-NO NOT = WS-CURRENT-ORDER
038800         PERFORM ECRIRE-LIGNE-PRODUIT
038900* CONVERSION DU CHAMP EDITE VERS NUMERIQUE POUR LE CALCUL
039000         MOVE EXT-LINE-TOTAL TO WS-LINE-TOTAL-WORK
039100         ADD WS-LINE-TOTAL-WORK TO WS-ORDER-TOTAL
039200         PERFORM LIRE-EXTRACT
039300     END-PERFORM
039400
039500* CALCULS ET TOTAUX
039600     PERFORM CALCULER-TOTAUX
039700     PERFORM ECRIRE-TOTAUX
039800
039900* FERMETURE DU CADRE
040000     MOVE LIGNE-CADRE-BAS TO FACTURE-RECORD
040100     PERFORM ECRIRE-LIGNE-FACTURE
040200
040300* SAUT DE PAGE
040400     PERFORM ECRIRE-SAUT-PAGE
040500     .
040600
040700 ECRIRE-ENTETE-FACTURE.
040800* CADRE ADRESSE - HAUT
040900     MOVE LIGNE-CADRE-ADRESSE-HAUT TO FACTURE-RECORD
041000     PERFORM ECRIRE-LIGNE-FACTURE
041100
041200* ADRESSE SOCIETE ENCADREE
041300     MOVE EXT-COMPANY TO LA-COMPANY
041400     MOVE LIGNE-ADRESSE TO FACTURE-RECORD
041500     PERFORM ECRIRE-LIGNE-FACTURE
041600
041700     MOVE EXT-ADDRESS TO LA-ADDRESS
041800     MOVE LIGNE-RUE TO FACTURE-RECORD
041900     PERFORM ECRIRE-LIGNE-FACTURE
042000
042100     MOVE EXT-CITY TO LA-CITY
042200     MOVE EXT-ZIP TO LA-ZIP
042300     MOVE LIGNE-VILLE TO FACTURE-RECORD
042400     PERFORM ECRIRE-LIGNE-FACTURE
042500
042600     MOVE EXT-STATE TO LA-STATE
042700     MOVE LIGNE-STATE TO FACTURE-RECORD
042800     PERFORM ECRIRE-LIGNE-FACTURE
042900
043000* CADRE ADRESSE - BAS
043100     MOVE LIGNE-CADRE-ADRESSE-BAS TO FACTURE-RECORD
043200     PERFORM ECRIRE-LIGNE-FACTURE
043300
043400     MOVE LIGNE-CADRE-VIDE TO FACTURE-RECORD
043500     PERFORM ECRIRE-LIGNE-FACTURE
043600
043700* DATE ET LIEU
043800     MOVE WS-DATE-FORMATEE TO LD-DATE-FORMATEE
043900     MOVE LIGNE-DATE TO FACTURE-RECORD
044000     PERFORM ECRIRE-LIGNE-FACTURE
044100
044200     MOVE LIGNE-CADRE-VIDE TO FACTURE-RECORD
044300     PERFORM ECRIRE-LIGNE-FACTURE
044400
044500* NUMERO DE COMMANDE
044600     MOVE EXT-O-NO TO LO-ORDER-NO
044700     MOVE LIGNE-ORDER TO FACTURE-RECORD
044800     PERFORM ECRIRE-LIGNE-FACTURE
044900
045000* DATE DE COMMANDE
045100     MOVE EXT-ODATE-ISO TO LOD-ORDER-DATE
045200     MOVE LIGNE-ORDER-DATE TO FACTURE-RECORD
045300     PERFORM ECRIRE-LIGNE-FACTURE
045400
045500     MOVE LIGNE-CADRE-VIDE TO FACTURE-RECORD
045600     PERFORM ECRIRE-LIGNE-FACTURE
045700
045800* CONTACT
045900     MOVE EXT-DNAME TO LC-DNAME
046000     MOVE EXT-LNAME TO LC-LNAME
046100     MOVE EXT-FNAME TO LC-FNAME
046200     MOVE LIGNE-CONTACT TO FACTURE-RECORD
046300     PERFORM ECRIRE-LIGNE-FACTURE
046400
046500     MOVE LIGNE-CADRE-VIDE TO FACTURE-RECORD
046600     PERFORM ECRIRE-LIGNE-FACTURE
046700
046800* EN-TETE DU TABLEAU AVEC CADRE INTERNE
046900     MOVE LIGNE-TABLEAU-HAUT TO FACTURE-RECORD
047000     PERFORM ECRIRE-LIGNE-FACTURE
047100
047200     MOVE LIGNE-HEADER TO FACTURE-RECORD
047300     PERFORM ECRIRE-LIGNE-FACTURE
047400     .
047500
047600 ECRIRE-LIGNE-PRODUIT.
047700     MOVE EXT-P-NO TO LP-P-NO
047800     MOVE EXT-DESCRIPTION TO LP-DESCRIPTION
047900* CONVERSION DES CHAMPS EDITES POUR L'AFFICHAGE
048000     MOVE EXT-QUANTITY TO WS-QUANTITY-WORK
048100     MOVE WS-QUANTITY-WORK TO LP-QUANTITY
048200     MOVE EXT-PRICE TO WS-PRICE-WORK
048300     MOVE WS-PRICE-WORK TO LP-PRICE
048400     MOVE EXT-LINE-TOTAL TO WS-LINE-TOTAL-WORK
048500     MOVE WS-LINE-TOTAL-WORK TO LP-LINE-TOTAL
048600
048700     MOVE LIGNE-PRODUIT TO FACTURE-RECORD
048800     PERFORM ECRIRE-LIGNE-FACTURE
048900     .
049000
049100 CALCULER-TOTAUX.
049200* CALCUL DE LA TVA
049300     COMPUTE WS-TVA-AMOUNT = WS-ORDER-TOTAL * WS-TVA-RATE
049400
049500* UTILISER FUNCTION NUMVAL POUR CONVERTIR CHAINE COM EN NUMERIQUE
049501*    COMPUTE WS-COMMISSION-RATE = FUNCTION NUMVAL(EXT-COM)
049502     MOVE EXT-COM TO WS-COMMISSION-RATE
049503
049504* CALCUL DE LA COMMISSION
049600     COMPUTE WS-COMMISSION-AMOUNT = WS-ORDER-TOTAL
049700                                            * WS-COMMISSION-RATE
049800
049900* CALCUL DU TOTAL AVEC TAXES
050000     COMPUTE WS-TOTAL-WITH-TAXES = WS-ORDER-TOTAL
050100                          + WS-TVA-AMOUNT
050200     .
050300
050400 ECRIRE-TOTAUX.
050500* CADRE FERMETURE TABLEAU
050600     MOVE LIGNE-TABLEAU-BAS TO FACTURE-RECORD
050700     PERFORM ECRIRE-LIGNE-FACTURE
050800
050900     MOVE LIGNE-CADRE-VIDE TO FACTURE-RECORD
051000     PERFORM ECRIRE-LIGNE-FACTURE
051100
051200* SOUS-TOTAL
051300     MOVE WS-ORDER-TOTAL TO LST-AMOUNT
051400     MOVE LIGNE-SOUS-TOTAL TO FACTURE-RECORD
051500     PERFORM ECRIRE-LIGNE-FACTURE
051600
051700* TVA AVEC POURCENTAGE
051800     MOVE WS-TVA-AMOUNT TO LT-AMOUNT
051900     MOVE WS-TVA-PERCENT TO LT-PERCENT
052000     MOVE LIGNE-TVA TO FACTURE-RECORD
052100     PERFORM ECRIRE-LIGNE-FACTURE
052200
052300* COMMISSION
052310     MOVE EXT-COM TO LC-COM
052400     MOVE WS-COMMISSION-AMOUNT TO LC-AMOUNT
052500     MOVE LIGNE-COMMISSION TO FACTURE-RECORD
052600     PERFORM ECRIRE-LIGNE-FACTURE
052700
052800     MOVE LIGNE-CADRE-VIDE TO FACTURE-RECORD
052900     PERFORM ECRIRE-LIGNE-FACTURE
053000
053100* TOTAL
053200     MOVE WS-TOTAL-WITH-TAXES TO LTO-AMOUNT
053300     MOVE LIGNE-TOTAL TO FACTURE-RECORD
053400     PERFORM ECRIRE-LIGNE-FACTURE
053500     .
053600
053700 ECRIRE-LIGNE-FACTURE.
053800     WRITE FACTURE-RECORD
053900     IF WS-FACTURE-STATUS NOT = '00'
054000         DISPLAY "ERREUR ECRITURE FACTURE : ", WS-FACTURE-STATUS
054100         PERFORM ABEND-PROG
054200     END-IF
054300     .
054400
054500 ECRIRE-SAUT-PAGE.
054600* ECRITURE DE QUELQUES LIGNES VIDES POUR SEPARATION
054700     MOVE LIGNE-VIDE TO FACTURE-RECORD
054800     PERFORM ECRIRE-LIGNE-FACTURE
054900     MOVE LIGNE-VIDE TO FACTURE-RECORD
055000     PERFORM ECRIRE-LIGNE-FACTURE
055100     MOVE LIGNE-VIDE TO FACTURE-RECORD
055200     PERFORM ECRIRE-LIGNE-FACTURE
055300     MOVE LIGNE-VIDE TO FACTURE-RECORD
055400     PERFORM ECRIRE-LIGNE-FACTURE
055500     .
055600
055700 ABEND-PROG.
055800     DISPLAY "ARRET ANORMAL DU PROGRAMME"
055900     MOVE 16 TO RETURN-CODE
056000     GOBACK
056100     .
